#import "../../Executable/MaPLGeneratorAPI.mapl"

string outputDir = commandLineFlag("swiftOutputDir");
outputToFile(outputDir + "/MaPLGeneratorSerialized.swift");

<?// Generated by MaPLGenerator.

import Foundation

enum MaPLGeneratorSerializedParseStatus {
    case success
    case error
    case completed
}

/**
 * Describes how generated objects are serialized / deserialized.
 */
protocol MaPLGeneratorSerialized {
    mutating func initializeObject(withData data: [UInt8], dataIndex: inout Int) -> Bool
    mutating func initializeAttribute(withData data: [UInt8], dataIndex: inout Int) -> MaPLGeneratorSerializedParseStatus
    func writeObject(toData data: inout Data)
    func writeAttributes(toData data: inout Data)
}

?>

for uint32 schemaIndex = 0; schemaIndex < schemas.count; schemaIndex++ {
    Schema schema = schemas[schemaIndex];
    string schemaNamespace = schema.namespace;

    for uint32 classIndex = 0; classIndex < schema.classes.count; classIndex++ {
        SchemaClass schemaClass = schema.classes[classIndex];
        string className = schemaClass.name;
        uint32 beginClassHash = (uint32)hash("CLASS_BEGIN_" + schemaNamespace + "_" + className);
        uint32 endClassHash = (uint32)hash("CLASS_END_" + schemaNamespace + "_" + className);
<?let BYTECODE_${schemaNamespace}_${className}_BEGIN: UInt32 = ${beginClassHash}
let BYTECODE_${schemaNamespace}_${className}_END: UInt32 = ${endClassHash}
?>
        
        for uint32 attrIndex = 0; attrIndex < schemaClass.attributes.count; attrIndex++ {
            SchemaAttribute attribute = schemaClass.attributes[attrIndex];
            string attributeName = attribute.name;
            uint32 attributeHash = (uint32)hash("ATTRIBUTE_" + schemaNamespace + "_" + className + "_" + attributeName);
            <?let BYTECODE_${schemaNamespace}_${className}_${attributeName}: UInt32 = ${attributeHash}
?>
        }
        <?
?>
    }
}
