#import "../../Executable/MaPLGeneratorAPI.mapl"

string outputDir = commandLineFlag("cSharpOutputDir");
outputToFile(outputDir + "/MaPLGeneratorSerialized.cs");

<?// Generated by MaPLGenerator.

using System.IO;

public enum MaPLGeneratorSerializedParseStatus {
    Success,
    Error,
    Completed,
}

/**
 * Describes how generated objects are serialized / deserialized.
 */
public interface MaPLGeneratorSerialized {
    public bool initializeObject(BinaryReader reader, uint classBytecode);
    public MaPLGeneratorSerializedParseStatus initializeAttribute(BinaryReader reader, uint attributeBytecode);
    public void writeObject(BinaryWriter writer);
    public void writeAttributes(BinaryWriter writer);

?>

for uint32 schemaIndex = 0; schemaIndex < schemas.count; schemaIndex++ {
    Schema schema = schemas[schemaIndex];
    string schemaNamespace = schema.namespace;

    for uint32 classIndex = 0; classIndex < schema.classes.count; classIndex++ {
        SchemaClass schemaClass = schema.classes[classIndex];
        string classHashName = schemaClass.annotations.contains("originalName") ? schemaClass.annotations["originalName"] : schemaClass.name;
        uint32 beginClassHash = (uint32)hash("CLASS_BEGIN_" + schemaNamespace + "_" + classHashName);
        uint32 endClassHash = (uint32)hash("CLASS_END_" + schemaNamespace + "_" + classHashName);
<?    const uint BYTECODE_${schemaNamespace}_${schemaClass.name}_BEGIN = ${beginClassHash};
    const uint BYTECODE_${schemaNamespace}_${schemaClass.name}_END = ${endClassHash};
?>
        
        for uint32 attrIndex = 0; attrIndex < schemaClass.attributes.count; attrIndex++ {
            SchemaAttribute attribute = schemaClass.attributes[attrIndex];
            string attributeHashName = attribute.annotations.contains("originalName") ? attribute.annotations["originalName"] : attribute.name;
            uint32 attributeHash = (uint32)hash("ATTRIBUTE_" + schemaNamespace + "_" + classHashName + "_" + attributeHashName);
<?    const uint BYTECODE_${schemaNamespace}_${schemaClass.name}_${attribute.name} = ${attributeHash};
?>
        }
        if classIndex < schema.classes.count-1 {
<?
?>
        }
    }
}
<?}
?>
